# -------------------------------
# Git prompt function
# -------------------------------
autoload -Uz vcs_info
precmd_vcs_info() { vcs_info }
precmd_functions+=( precmd_vcs_info )
setopt prompt_subst

# Configure vcs_info for git
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' get-unapplied true
zstyle ':vcs_info:*' stagedstr '%F{green}%f'
zstyle ':vcs_info:*' unstagedstr '%F{208}%f' # orange color
zstyle ':vcs_info:git:*' formats ' %F{magenta} %b %f%c%u%m'
zstyle ':vcs_info:git:*' actionformats ' %F{magenta} %b%f%F{yellow}|%a%f%c%u'
# Enable git hooks
zstyle ':vcs_info:git*+set-message:*' hooks git-untracked git-behind

# Hook function to check if behind origin
+vi-git-behind() {
    local behind
    behind=$(git rev-list HEAD..@{upstream} 2>/dev/null | wc -l)
    if [[ $behind -gt 0 ]]; then
        hook_com[misc]="%F{yellow}↓%f"
    fi
}

# Hook function to show untracked files
+vi-git-untracked() {
    if [[ $(git rev-parse --is-inside-work-tree 2>/dev/null) == 'true' ]] && \
       git status --porcelain 2>/dev/null | grep -q '^??'; then
        hook_com[staged]="%F{white}%B+%b%f${hook_com[staged]}"
    fi
}

# Auto-fetch when entering a git repo
chpwd_git_fetch() {
    if git rev-parse --is-inside-work-tree > /dev/null 2>&1; then
        {
            git fetch --quiet 2>/dev/null
            vcs_info
            if (( $+widgets[zle-line-init] )); then
                zle -R
            fi
        } &!
    fi
}
chpwd_functions+=( chpwd_git_fetch )

# -------------------------------
# Vi mode indicator
# -------------------------------
function zle-line-init zle-keymap-select {
    VI_MODE_SYMBOL="${${KEYMAP/vicmd/}/(main|viins)/}"
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

# -------------------------------
# Custom prompt
# -------------------------------
# Basic two-line prompt with user@host, directory, and git info
PROMPT='%F{magenta}%n@%m%f %F{cyan}%~%f${vcs_info_msg_0_}
%F{%(?.green.red)}${VI_MODE_SYMBOL}%f '
