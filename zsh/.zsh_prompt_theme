# -------------------------------
# Git prompt function
# -------------------------------
autoload -Uz vcs_info
precmd_vcs_info() { vcs_info }
precmd_functions+=( precmd_vcs_info )
setopt prompt_subst

# Configure vcs_info for git
zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr '%F{green}●%f'
zstyle ':vcs_info:*' unstagedstr '%F{white}●%f'
zstyle ':vcs_info:git:*' formats ' %F{magenta} %b %f%c%u%m'
zstyle ':vcs_info:git:*' actionformats ' %F{magenta} %b%f%F{yellow}|%a%f%c%u'
# Enable git hooks
zstyle ':vcs_info:git*+set-message:*' hooks git-behind

# Hook function to check if behind origin
+vi-git-behind() {
    local behind
    behind=$(git rev-list HEAD..@{upstream} 2>/dev/null | wc -l)
    if [[ $behind -gt 0 ]]; then
        hook_com[misc]="%F{yellow}↓%f"
    fi
}

# Auto-fetch when entering a git repo
chpwd_git_fetch() {
    if git rev-parse --is-inside-work-tree > /dev/null 2>&1; then
        {
            git fetch --quiet 2>/dev/null
            vcs_info
            if (( $+widgets[zle-line-init] )); then
                zle -R
            fi
        } &!
    fi
}
chpwd_functions+=( chpwd_git_fetch )

# -------------------------------
# Vi mode indicator
# -------------------------------
function zle-line-init zle-keymap-select {
    VI_MODE_SYMBOL="${${KEYMAP/vicmd/}/(main|viins)/}"
    zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

# -------------------------------
# Custom prompt
# -------------------------------
# Basic two-line prompt with user@host, directory, and git info
PROMPT='%F{magenta}%n@%m%f %F{cyan}%~%f${vcs_info_msg_0_}
%F{%(?.green.red)}${VI_MODE_SYMBOL}%f '
